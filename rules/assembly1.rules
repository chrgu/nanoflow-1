####################
## Hybrid Assembly Pipeline 1:
## Canu + Nanopolish (+ Circlator + Pilon)
####################

rule all_draft1:
 input:
  expand(config['project_dir'] + '/reports/{step}/{barcode}/asm.aln.tsv', 
    step=['04_canu','05_nanopolish'], barcode=BARCODES),
  expand(config['project_dir'] + '/07_pilon/{barcode}/asm.aln.paf', barcode=BARCODES)

rule long_read_assembly:
  input:
    expand(config['project_dir'] + '/reports/{step}/{barcode}/asm.aln.tsv',
      step=['05_nanopolish'], barcode=BARCODES)

rule pilon_on_nanopolish:
 input:
  expand(config['project_dir'] + '/07_pilon/{barcode}/asm.aln.paf', barcode=BARCODES)

#### Long Reads Assembler: Canu
rule canu_asm:
 input:
  reads = config['project_dir'] + '/03_subsampled_reads/{barcode}/reads.fastq.gz'
 output:
  config['project_dir'] + '/04_canu/{barcode}/canu.contigs.fasta',
  config['project_dir'] + '/04_canu/{barcode}/canu.correctedReads.fasta.gz'
 params:
  outdir = config['project_dir'] + '/04_canu/{barcode}'
 threads: 8
 shell:
  """
  LD_LIBRARY_PATH="$CONDA_PREFIX/lib64" canu \
   -p canu -d {params.outdir} genomeSize={config[canu_genome_size]} \
   -nanopore-raw {input.reads} correctedErrorRate=0.16 useGrid=false \
   maxMemory=20G maxThreads={threads}
  """

rule assess_canu:
 input:
  genome = config['project_dir'] + '/' + config['genome_fp'],
  contigs = config['project_dir'] + '/04_canu/{barcode}/canu.contigs.fasta',
  python_chop_fp = config['basecalling_cmp_fp'] + '/chop_up_assembly.py',
  python_ident_fp = config['basecalling_cmp_fp'] + '/read_length_identity.py'
 output:
  asm_pieces = config['project_dir'] + '/04_canu/{barcode}/asm_pieces.fasta',
  asm_aln = config['project_dir'] + '/04_canu/{barcode}/asm.aln.paf',
  asm_table = config['project_dir'] + '/reports/04_canu/{barcode}/asm.aln.tsv'
 threads: 4
 shell:
  """
  python {input.python_chop_fp} {input.contigs} 10000 > {output.asm_pieces}
  minimap2 -k12 -t {threads} -c {input.genome} {output.asm_pieces} > {output.asm_aln}
  python {input.python_ident_fp} {output.asm_pieces} {output.asm_aln} > {output.asm_table}
  """

#### Prepare sequencing summary for fast nanopolish index fast5 files
rule collect_sequencing_summary:
 input:
  config['basecalled_fast5_fp1'] + '/sequencing_summary.txt',
  config['basecalled_fast5_fp2'] + '/sequencing_summary.txt'
 output:
  config['project_dir'] + '/05_nanopolish/{barcode}/sequencing_summary_files.txt'
 run:
  with open(output[0],'w') as out:
   for item in input:
    out.write("%s\n" % os.path.abspath(item))

## Index the reads after preprocessing
rule nanopolish_index:
 input:
  fast5_fp1 = config['basecalled_fast5_fp1'] + '/workspace/pass/{barcode}',
  fast5_fp2 = config['basecalled_fast5_fp2'] + '/workspace/pass/{barcode}',
  reads = config['project_dir'] + '/03_subsampled_reads/{barcode}/reads.fastq.gz',
  nanopolish_fp = config['nanopolish_fp'] + '/nanopolish',
  summary = config['project_dir'] + '/05_nanopolish/{barcode}/sequencing_summary_files.txt'
 output:
  config['project_dir'] + '/03_subsampled_reads/{barcode}/reads.fastq.gz.index.readdb'
 shell:
  """
  if [[ {input.fast5_fp1} -ef {input.fast5_fp2} ]]; then
   LD_LIBRARY_PATH="$CONDA_PREFIX/lib64" {input.nanopolish_fp} index -d {input.fast5_fp1} -f {input.summary} {input.reads}
  else
   LD_LIBRARY_PATH="$CONDA_PREFIX/lib64" {input.nanopolish_fp} index -d {input.fast5_fp1} -d {input.fast5_fp2} -f {input.summary} {input.reads}
  fi
  """

## Switch to Minimap2 alignment from BWA
rule minimap2_align_nanopolish:
 input:
  contigs_canu = config['project_dir'] + '/04_canu/{barcode}/canu.contigs.fasta',
  reads = config['project_dir'] + '/03_subsampled_reads/{barcode}/reads.fastq.gz',
  reads_index = config['project_dir'] + '/03_subsampled_reads/{barcode}/reads.fastq.gz.index.readdb',
  summary = config['project_dir'] + '/05_nanopolish/{barcode}/sequencing_summary_files.txt'
 output:
  contigs = config['project_dir'] + '/05_nanopolish/{barcode}/canu.contigs.fasta',
  sorted_bam = config['project_dir'] + '/05_nanopolish/{barcode}/reads.sorted.bam',
  sorted_bai = config['project_dir'] + '/05_nanopolish/{barcode}/reads.sorted.bam.bai'
 params:
  temp = config['project_dir'] + '/05_nanopolish/{barcode}/reads.tmp'
 threads: 8
 shell:
  """
  cp {input.contigs_canu} {output.contigs}
  minimap2 -ax map-ont -t {threads} {output.contigs} {input.reads} | \
   samtools sort -o {output.sorted_bam} -T {params.temp} -
  samtools index {output.sorted_bam}
  """

## Polish a genome assembly
rule nanopolish_consensus:
 input:
  reads = config['project_dir'] + '/03_subsampled_reads/{barcode}/reads.fastq.gz',
  bam = config['project_dir'] + '/05_nanopolish/{barcode}/reads.sorted.bam',
  bai = config['project_dir'] + '/05_nanopolish/{barcode}/reads.sorted.bam.bai',
  contigs = config['project_dir'] + '/05_nanopolish/{barcode}/canu.contigs.fasta',
  makerange_fp = config['nanopolish_fp'] + '/scripts/nanopolish_makerange.py',
 output:
  config['project_dir'] + '/05_nanopolish/{barcode}/nanopolish.contigs.fasta'
 params:
  results_fp = config['project_dir'] + '/05_nanopolish/{barcode}/nanopolish.results',
  polished_fp = config['project_dir'] + '/05_nanopolish/{barcode}/polished.results'
 threads: 8
 shell:
  """
  mkdir -p {params.polished_fp}
  python {input.makerange_fp} {input.contigs} | \
   parallel --results {params.results_fp} -P 4 \
    LD_LIBRARY_PATH="$CONDA_PREFIX/lib64" {config[nanopolish_fp]}/nanopolish variants \
    --consensus -o {params.polished_fp}/polished.{{1}}.vcf \
    -w {{1}} -r {input.reads} -b {input.bam} -g {input.contigs} \
    -t {threads} --min-candidate-frequency 0.1 --methylation-aware=dcm,dam
  nanopolish vcf2fasta -g {input.contigs} {params.polished_fp}/polished.*.vcf > {output}
  """

rule assess_nanopolish:
 input:
  genome = config['project_dir'] + '/' + config['genome_fp'],
  contigs_nanopolish = config['project_dir'] + '/05_nanopolish/{barcode}/nanopolish.contigs.fasta',
  python_chop_fp = config['basecalling_cmp_fp'] + '/chop_up_assembly.py',
  python_ident_fp = config['basecalling_cmp_fp'] + '/read_length_identity.py'
 output:
  asm_pieces = config['project_dir'] + '/05_nanopolish/{barcode}/asm_pieces.fasta',
  asm_aln = config['project_dir'] + '/05_nanopolish/{barcode}/asm.aln.paf',
  asm_table = config['project_dir'] + '/reports/05_nanopolish/{barcode}/asm.aln.tsv'
 threads: 8
 shell:
  """
  python {input.python_chop_fp} {input.contigs_nanopolish} 10000 > {output.asm_pieces}
  minimap2 -x map10k -t {threads} -c {input.genome} {output.asm_pieces} > {output.asm_aln}
  python {input.python_ident_fp} {output.asm_pieces} {output.asm_aln} > {output.asm_table}
  """

#### Circlator: trim overhangs and circularize contigs
rule run_circlator:
 input:
  contigs = config['project_dir'] + '/05_nanopolish/{barcode}/nanopolish.contigs.fasta',
  correctedReads = config['project_dir'] + '/04_canu/{barcode}/canu.correctedReads.fasta.gz'
 output:
  config['project_dir'] + '/06_circlator/.{barcode}_done_circlator',
 params:
  outdir = config['project_dir'] + '/06_circlator/{barcode}',
  contigs = config['project_dir'] + '/06_circlator/{barcode}/06.fixstart.fasta'
 threads: 4
 shell:
  """
  circlator all --merge_min_id 85 --merge_breaklen 1000 --threads {threads} --verbose \
  --assembler canu --data_type nanopore-corrected --bwa_opts "-x ont2d" \
   {input.contigs} {input.correctedReads} {params.outdir}
  touch {output}
  """

#### Pilon: improve draft assembly using short reads
rule minimap2_align_pilon:
 input:
  R1 = config['project_dir'] + '/' + config['short_reads_fp'] + '/{barcode}/R1.fastq',
  R2 = config['project_dir'] + '/' + config['short_reads_fp'] + '/{barcode}/R2.fastq',
  reads = config['project_dir'] + '/03_subsampled_reads/{barcode}/reads.fastq.gz',
  reads_index = config['project_dir'] + '/03_subsampled_reads/{barcode}/reads.fastq.gz.index.readdb',
 output:
  contigs = config['project_dir'] + '/07_pilon/{barcode}/contigs.fasta',
  sorted_bam = config['project_dir'] + '/07_pilon/{barcode}/reads.sorted.bam',
  sorted_bai = config['project_dir'] + '/07_pilon/{barcode}/reads.sorted.bam.bai'
 params:
  contigs = config['project_dir'] + '/05_nanopolish/{barcode}/nanopolish.contigs.fasta',
 # contigs = config['project_dir'] + '/05_circlator/{barcode}/06.fixstart.fasta',
  temp = config['project_dir'] + '/07_pilon/{barcode}/reads.tmp'
 threads: 8
 shell:
  """
  cp {params.contigs} {output.contigs}
  minimap2 -ax map-ont -t {threads} {output.contigs} {input.reads} | \
   samtools sort -o {output.sorted_bam} -T {params.temp} -
  samtools index {output.sorted_bam}
  """

rule run_pilon:
 input:
  contigs = config['project_dir'] + '/07_pilon/{barcode}/contigs.fasta',
  aln_bam = config['project_dir'] + '/07_pilon/{barcode}/reads.sorted.bam',
  aln_bai = config['project_dir'] + '/07_pilon/{barcode}/reads.sorted.bam.bai'
 output:
  draft_v1 = config['project_dir'] + '/07_pilon/{barcode}/pilon.fasta'
 params:
  outdir = config['project_dir'] + '/07_pilon/{barcode}'
 shell:
  """
  pilon --genome {input.contigs} --frags {input.aln_bam} \
   --fix bases --changes --tracks \
   --outdir {params.outdir}
  """

rule assess_pilon:
 input:
  genome = config['project_dir'] + '/' + config['genome_fp'],
  draft_v1 = config['project_dir'] + '/07_pilon/{barcode}/pilon.fasta',
  python_chop_fp = config['basecalling_cmp_fp'] + '/chop_up_assembly.py',
  python_ident_fp = config['basecalling_cmp_fp'] + '/read_length_identity.py'
 output:
  asm_pieces = config['project_dir'] + '/07_pilon/{barcode}/asm_pieces.fasta',
  asm_aln = config['project_dir'] + '/07_pilon/{barcode}/asm.aln.paf',
  asm_table = config['project_dir'] + '/reports/07_pilon/{barcode}/asm.aln.tsv'
 threads: 8
 shell:
  """
  python {input.python_chop_fp} {input.draft_v1} 10000 > {output.asm_pieces}
  minimap2 -x map10k -t {threads} -c {input.genome} {output.asm_pieces} > {output.asm_aln}
  python {input.python_ident_fp} {output.asm_pieces} {output.asm_aln} > {output.asm_table}
  """
